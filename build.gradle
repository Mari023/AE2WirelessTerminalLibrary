buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "fabric-loom" version "1.0-SNAPSHOT"
    id "com.diffplug.spotless" version "5.12.4"
    id 'maven-publish'
}

version = project.mod_version + "-SNAPSHOT"

ext.pr = System.getenv('PR_NUMBER') ?: ""
if (ext.pr) {
    version = project.mod_version + "+pr" + ext.pr
}

ext.tag = System.getenv('TAG') ?: ""
if (ext.tag) {
    if (!ext.tag.startsWith("${rootProject.modloader}/")) {
        throw new GradleException("Tags for the ${rootProject.modloader} version should start with ${rootProject.modloader}/: ${ext.tag}")
    }
    version = ext.tag.substring("${rootProject.modloader}/".length())
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modCompileOnly("dev.emi:trinkets:${rootProject.trinkets_version}") { transitive false }
    modCompileOnly("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
    //modRuntimeOnly "dev.emi:trinkets:${rootProject.trinkets_version}"
    modRuntimeOnly("me.shedaniel.cloth:cloth-config-${rootProject.modloader}:${project.cloth}")
    modApi("com.terraformersmc:modmenu:${project.mod_menu_version}")
    modImplementation "dev.architectury:architectury-${rootProject.modloader}:${rootProject.architectury_version}"
    modImplementation("appeng:appliedenergistics2-${rootProject.modloader}:${rootProject.ae2_version}") {
        exclude group: "mezz.jei"
        exclude group: "me.shedaniel"
        exclude group: "net.fabricmc.fabric-api"
    }

    if (project.runtime_itemlist_mod == "jei") {
        modImplementation("mezz.jei:jei-${jei_minecraft_version}-${rootProject.modloader}:${jei_version}") {
            exclude group: "mezz.jei"
        }

        modCompileOnly("me.shedaniel:RoughlyEnoughItems-${rootProject.modloader}:${project.rei_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    } else if (project.runtime_itemlist_mod == "rei") {
        modCompileOnly("mezz.jei:jei-${jei_minecraft_version}-${rootProject.modloader}:${jei_version}") {
            exclude group: "mezz.jei"
        }

        modImplementation("me.shedaniel:RoughlyEnoughItems-${rootProject.modloader}:${project.rei_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    } else {
        modCompileOnly("mezz.jei:jei-${jei_minecraft_version}-${rootProject.modloader}:${jei_version}") {
            exclude group: "mezz.jei"
        }

        modCompileOnly("me.shedaniel:RoughlyEnoughItems-${rootProject.modloader}:${project.rei_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    }

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}


archivesBaseName = rootProject.archives_base_name
version = rootProject.version
group = rootProject.maven_group

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        content {
            includeGroup "net.fabricmc.fabric-api"
            includeGroup "appeng"
            includeGroup "mezz.jei"
        }
    }
    maven {
        url "https://maven.bai.lol"
        content {
            includeGroup "mcp.mobius.waila"
            includeGroup "lol.bai"
        }
    }
    maven {
        url "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
            includeGroup "dev.architectury"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup "com.terraformersmc"
            includeGroup "dev.emi"
        }
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://maven.ladysnake.org/releases"
        content {
            includeGroup "dev.onyxstudios.cardinal-components-api"
        }
    }
    maven {
        url "https://maven.parchmentmc.net/"
        content {
            includeGroup "org.parchmentmc.data"
        }
    }
    maven {
        url = "https://maven.theillusivec4.top/"
        content {
            includeGroup "top.theillusivec4.curios"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    withSourcesJar()
}

jar {
    finalizedBy 'remapJar'
}

processResources {
    inputs.property "version", project.version
    inputs.property "ae2_version", rootProject.ae2_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "ae2_version": rootProject.ae2_version
    }
}

spotless {
    buildscript {
        repositories {
            mavenCentral()
        }
    }

    java {
        target '/src/*/java/**/*.java'

        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile 'codeformat/codeformat.xml'
        importOrderFile 'codeformat/ae2wtlib.importorder'
    }

    format 'json', {
        target '*/src/*/resources/**/*.json'
        prettier().config(['parser': 'json'])
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'ae2wtlib-fabric'
            version = project.version

            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
