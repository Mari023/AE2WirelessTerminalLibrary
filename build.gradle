plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version '1.1.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = project.mod_version + "-SNAPSHOT"
versionType = project.versionType
boolean shouldPublish = false

ext.pr = System.getenv('PR_NUMBER') ?: ""
if (ext.pr) {
    version = project.mod_version + "+pr" + ext.pr
}

ext.tag = System.getenv('TAG') ?: ""
if (ext.tag) {
    version = ext.tag
    if (project.version.contains("alpha")) {
        versionType = "alpha"
        shouldPublish = true
    }
    if (project.version.contains("beta")) {
        versionType = "beta"
        shouldPublish = true
    }
    if (project.version.contains("release")) {
        versionType = "release"
        shouldPublish = true
    }
}

archivesBaseName = project.archives_base_name
group = project.maven_group

repositories {
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
    }
    // Required for libblockattributes
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
        content {
            includeGroup "alexiil.mc.lib"
        }
    }
    maven {
        name = "Technici4n"
        url = "https://raw.githubusercontent.com/Technici4n/Technici4n-maven/master/"
        content {
            includeGroup "dev.technici4n"
            includeGroup "net.fabricmc.fabric-api" // until ItemApiLookup PR #1352 is merged.
        }
    }
    maven {
        url = "https://raw.githubusercontent.com/TerraformersMC/Archive/main/releases/"
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.shedaniel.me/"
    }
    maven {
        name "Siphalor's Maven"
        url "https://maven.siphalor.de"
    }
    maven {
        name = "NerdHubMC"
        url = "https://maven.abusedmaster.xyz/"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
    modImplementation "appeng:appliedenergistics2-fabric:${project.ae2_version}", {
        exclude group: "FastTransferLib"
    }
    modImplementation "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"
    modImplementation "curse.maven:minemenufabric-478687:${project.minemenufabric_version}"
    modImplementation "com.terraformersmc:modmenu:1.16.7"//dependency for minemenu
    modApi("me.shedaniel.cloth:cloth-config-fabric:4.11.14")//dependency for minemenu
    modImplementation "dev.emi:trinkets:${project.trinkets_version}"
    modRuntime "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.8.3"
    modApi("dev.technici4n:FastTransferLib:${project.ftl_version}", {
        exclude group: "dev.technici4n"
    })
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth(type: TaskModrinthUpload) {
    if (shouldPublish) {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = 'pNabrMMw'
        uploadFile = remapJar
        addGameVersion('1.16.5')
        addLoader('fabric')
        versionNumber = project.version
        releaseType = versionType
        changelog = System.getenv("CHANGELOG") ?: "Please visit our [releases](https://github.com/Mari023/AE2WirelessTerminalLibrary/releases) for a changelog"
    }
}

curseforge {
    if (shouldPublish) {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        project {
            id = '459929'
            changelogType = "markdown"
            changelog = System.getenv("CHANGELOG") ?: "Please visit our [releases](https://github.com/Mari023/AE2WirelessTerminalLibrary/releases) for a changelog"
            releaseType = versionType
            addGameVersion '1.16.5'
            addGameVersion 'Fabric'
            addGameVersion "Java 8"
            mainArtifact(remapJar) {
                displayName = project.version
            }

            relations {
                requiredDependency 'fabric-api'
                requiredDependency 'applied-energistics-2'
                optionalDependency 'roughly-enough-items'
                optionalDependency 'minemenufabric'
                optionalDependency 'trinkets-fabric'
            }
            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

jar {
    from "LICENSE"
}
